#summary VCL Styles for Inno Setup.

= Introduction =

 This plugin (dll) allow to skin the installers created by  <a href="http://www.jrsoftware.org/isinfo.php">Inno setup</a>. The current size of the plugin is about 1.6 mb, but when is included (and compressed) in the script only add ~500 Kb to the final installer.


= Installation =

The installer for the plugin includes 30+ VCL Styles, a set of wizard images and samples inno scripts. To install just follow the installer instructions.

https://theroadtodelphi.files.wordpress.com/2014/07/1.png

https://theroadtodelphi.files.wordpress.com/2014/07/21.png

https://theroadtodelphi.files.wordpress.com/2014/07/3.png

https://theroadtodelphi.files.wordpress.com/2014/07/41.png

https://theroadtodelphi.files.wordpress.com/2014/07/5.png

= Screenshots =

https://theroadtodelphi.files.wordpress.com/2014/07/11.png
https://theroadtodelphi.files.wordpress.com/2014/07/22.png
https://theroadtodelphi.files.wordpress.com/2014/07/31.png
https://theroadtodelphi.files.wordpress.com/2014/07/41.png
https://theroadtodelphi.files.wordpress.com/2014/07/51.png
https://theroadtodelphi.files.wordpress.com/2014/07/6.png
https://theroadtodelphi.files.wordpress.com/2014/07/7.png

= How to use it =

In order to use the plugin you must follow these steps

   # Add the !VclStylesinno.dll file to your inno setup script and the VCL Style file (skin) to use.
   # Import the function _LoadVCLStyleW_ for Unicode versions of Inno setup or the _LoadVCLStyleA_ method for the Ansi version
   # Import the function UnLoadVCLStyles
   # In the !InitializeSetup function extract the style to use and call the _LoadVCLStyle_ method passing the name of the style file
   # Finally in the !DeinitializeSetup function call the _UnLoadVCLStyles_ method.

Check the next sample script

{{{
[Files]
Source: ..\VclStylesinno.dll; DestDir: {app}; Flags: dontcopy
Source: ..\Styles\Amakrits.vsf; DestDir: {app}; Flags: dontcopy
 
[Code]
// Import the LoadVCLStyle function from VclStylesInno.DLL
procedure LoadVCLStyle(VClStyleFile: String); external 'LoadVCLStyleW@files:VclStylesInno.dll stdcall';
// Import the UnLoadVCLStyles function from VclStylesInno.DLL
procedure UnLoadVCLStyles; external 'UnLoadVCLStyles@files:VclStylesInno.dll stdcall';
 
function InitializeSetup(): Boolean;
begin
  ExtractTemporaryFile('Amakrits.vsf');
  LoadVCLStyle(ExpandConstant('{tmp}\Amakrits.vsf'));
  Result := True;
end;
 
procedure DeinitializeSetup();
begin
  UnLoadVCLStyles;
end;

}}}

In order to skin the uninstaller you must use a script like this.

{{{
#define VCLStylesSkinPath "{localappdata}\VCLStylesSkin"
[Files]
Source: ..\VclStylesinno.dll; DestDir: {#VCLStylesSkinPath}; Flags: uninsneveruninstall
Source: ..\Styles\Amakrits.vsf; DestDir: {#VCLStylesSkinPath}; Flags: uninsneveruninstall


[Code]
// Import the LoadVCLStyle function from VclStylesInno.DLL
procedure LoadVCLStyle(VClStyleFile: String); external 'LoadVCLStyleW@files:VclStylesInno.dll stdcall setuponly';
procedure LoadVCLStyle_UnInstall(VClStyleFile: String); external 'LoadVCLStyleW@{#VCLStylesSkinPath}\VclStylesInno.dll stdcall uninstallonly';
// Import the UnLoadVCLStyles function from VclStylesInno.DLL
procedure UnLoadVCLStyles; external 'UnLoadVCLStyles@files:VclStylesInno.dll stdcall setuponly';
procedure UnLoadVCLStyles_UnInstall; external 'UnLoadVCLStyles@{#VCLStylesSkinPath}\VclStylesInno.dll stdcall uninstallonly';

function InitializeSetup(): Boolean;
begin
 ExtractTemporaryFile('Amakrits.vsf');
 LoadVCLStyle(ExpandConstant('{tmp}\Amakrits.vsf'));
 Result := True;
end;

procedure DeinitializeSetup();
begin
	UnLoadVCLStyles;
end;

function InitializeUninstall: Boolean;
begin
  Result := True;
  LoadVCLStyle_UnInstall(ExpandConstant('{#VCLStylesSkinPath}\Amakrits.vsf'));
end;

procedure DeinitializeUninstall();
begin
  UnLoadVCLStyles_UnInstall;
end;
}}}

= Installer =

 Download the installer of the plugin from <a href="http://goo.gl/Exkewk">here</a>. The current version of the plugin is 1.3.2.9 release Date 20141123